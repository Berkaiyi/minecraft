import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'launcher.Main'

    if (OperatingSystem.current().isMacOsX()) {
        applicationDefaultJvmArgs = ['-XstartOnFirstThread']
    }
}

repositories {
    mavenCentral()
}

ext {
    lwjglVersion = "3.3.6"

    def os = OperatingSystem.current()
    if (os.isWindows()) {
        lwjglNatives = "natives-windows"
    }
    else if (os.isMacOsX()) {
        lwjglNatives = "natives-macos-arm64"
    }
    else {
        lwjglNatives = "natives-linux"
    }
}

dependencies {
    // LWJGL BOM
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    // LWJGL Module
    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-opengl"

    // LWJGL Natives
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}